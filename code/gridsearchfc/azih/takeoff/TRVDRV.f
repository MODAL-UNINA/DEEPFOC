      SUBROUTINE TRVDRV(V,D,DEPTH,VSQ,NL,THK,H,G,F,TID,DID,
     &  DELTA,Z,ZSQ,NR,T,ANIN)
C------- COMPUTE TRAVEL TIME AND DERIVATIVES FROM CRUSTAL MODEL --------
C MODAL
      INTEGER NL,NR
      REAL*4 Z, ZSQ
C MODAL
      INTEGER NLMAX,ISW
      PARAMETER (NLMAX=119)
c      CHARACTER*4 ISW
      INTEGER*4 KDX(202)
      REAL*4 DELTA(202),DX(202),DY(202),T(202),ANIN(202)
      REAL*4 V(NLMAX),D(NLMAX),VSQ(NLMAX),THK(NLMAX),H(NLMAX)
      REAL*4 DEPTH(NLMAX)
      REAL*4 TINJ(NLMAX),DIDJ(NLMAX),TR(NLMAX),TID(NLMAX,NLMAX)
      REAL*4 DID(NLMAX,NLMAX),F(NLMAX,NLMAX)
      REAL*4 G(4,NLMAX),FLT(100,100)

      ISW = 2

C-----------------------------------------------------------------------
      IF (ISW .EQ.1) GO TO 5
C-----INITIALIZATION FOR FIXED LAYER MODEL -----------------------------
      DO  1 L=1,NL
      IF (D(L) .GT. Z) GO TO  2
    1 CONTINUE
      JL=NL
      GO TO  3
    2 JJ=L
      JL=L-1
    3 TKJ=Z-D(JL)
      TKJSQ=TKJ**2+0.000001
      IF (JL .EQ. NL) GO TO  5
      DO  4 L=JJ,NL
      SQT=SQRT(VSQ(L)-VSQ(JL))
      TINJ(L)=TID(JL,L)-TKJ*SQT/(V(L)*V(JL))
    4 DIDJ(L)=DID(JL,L)-TKJ*V(JL)/SQT
      XOVMAX=V(JJ)*V(JL)*(TINJ(JJ)-TID(JL,JL))/(V(JJ)-V(JL))
c      write(*,*) SQT,TINJ(L),DIDJ(L),XOVMAX
    5 DO 300 I=1,NR
      IF (ISW .NE.1) GO TO 45
C-----INITIALIZATION FOR VARIABLE LAYER MODEL --------------------------
      JI=KDX(I)
      DEPTH(2)=FLT(KNO,JI)
      IF (Z .LT. FLTEP) DEPTH(2)=0.5*(FLT(KNO,JI)+FLTEP)
      THK(1)=DEPTH(2)
      THK(2)=D(3)-DEPTH(2)
      DH1=THK(1)-H(1)
      DH2=THK(2)-H(2)
      DO 10 L=1,NL
      IF (DEPTH(L) .GT. Z) GO TO 20
   10 CONTINUE
      JL=NL
      GO TO 30
   20 JJ=L
      JL=L-1
   30 TKJ=Z-DEPTH(JL)
      TKJSQ=TKJ**2+0.000001
      IF (JL .EQ. NL) GO TO 100
C-----CALCULATION FOR REFRACTED WAVES ----------------------------------
      DO 40 L=JJ,NL
      SQT=SQRT(VSQ(L)-VSQ(JL))
      TIX=F(1,JL)*DH1*G(1,L)+F(2,JL)*DH2*G(2,L)+TID(JL,L)
      DIX=F(1,JL)*DH1*G(3,L)+F(2,JL)*DH2*G(4,L)+DID(JL,L)
      TINJ(L)=TIX-TKJ*SQT/(V(L)*V(JL))
   40 DIDJ(L)=DIX-TKJ*V(JL)/SQT
      TIX=F(1,JL)*DH1*G(1,JL)+F(2,JL)*DH2*G(2,JL)+TID(JL,JL)
      XOVMAX=V(JJ)*V(JL)*(TINJ(JJ)-TIX)/(V(JJ)-V(JL))
      GO TO 50
   45 IF (JL .EQ. NL) GO TO 100
   50 DO 60 M=JJ,NL
   60 TR(M)=TINJ(M)+DELTA(I)/V(M)
      TMIN=999.99
      DO 70 M=JJ,NL
      IF (TR(M) .GT. TMIN) GO TO 70
      IF (DIDJ(M) .GT. DELTA(I)) GO TO 70
      K=M
      TMIN=TR(M)
   70 CONTINUE
      IF (DELTA(I) .LT. XOVMAX) GO TO 90
C-----TRAVEL TIME & DERIVATIVES FOR REFRACTED WAVE
   80 T(I)=TR(K)
      DTDD=1.0/V(K)
      DTDH=-SQRT(VSQ(K)-VSQ(JL))/(V(K)*V(JL))
      ANIN(I)=-V(JL)/V(K)
      GO TO 260
C-----CALCULATION FOR DIRECT WAVE --------------------------------------
   90 IF (JL .NE. 1) GO TO 100
      SQT=SQRT(ZSQ+DELTA(I)**2)
      TDJ1=SQT/V(1)
      IF (TDJ1 .GE. TMIN) GO TO 80
C-----TRAVEL TIME & DERIVATIVES FOR DIRECT WAVE IN FIRST LAYER
      T(I)=TDJ1
      DTDD=DELTA(I)/(V(1)*SQT)
      DTDH=Z/(V(1)*SQT)
      ANIN(I)=DELTA(I)/SQT
      GO TO 260
C-----FIND A DIRECT WAVE THAT WILL EMERGE AT THE STATION
  100 XBIG=DELTA(I)
      XLIT=DELTA(I)*TKJ/Z
      UB=XBIG/SQRT(XBIG**2+TKJSQ)
      UL=XLIT/SQRT(XLIT**2+TKJSQ)
      UBSQ=UB**2
      ULSQ=UL**2
      DELBIG=TKJ*UB/SQRT(1.000001-UBSQ)
      DELLIT=TKJ*UL/SQRT(1.000001-ULSQ)
      J1=JL-1
      DO 110 L=1,J1
      DELBIG=DELBIG+(THK(L)*UB)/SQRT(VSQ(JL)/VSQ(L)-UBSQ)
  110 DELLIT=DELLIT+(THK(L)*UL)/SQRT(VSQ(JL)/VSQ(L)-ULSQ)
      DO 170 LL=1,25
      IF (DELBIG-DELLIT .LT. 0.02) GO TO 180
      XTR=XLIT+(DELTA(I)-DELLIT)*(XBIG-XLIT)/(DELBIG-DELLIT)
      U=XTR/SQRT(XTR**2+TKJSQ)
      USQ=U**2
      DELXTR=TKJ*U/SQRT(1.000001-USQ)
      DO 120 L=1,J1
  120 DELXTR=DELXTR+(THK(L)*U)/SQRT(VSQ(JL)/VSQ(L)-USQ)
      XTEST=DELTA(I)-DELXTR
      IF (ABS(XTEST) .LE. 0.02) GO TO 190
      IF (XTEST) 140,190,150
  140 XBIG=XTR
      DELBIG=DELXTR
      GO TO 160
  150 XLIT=XTR
      DELLIT=DELXTR
  160 IF (LL .LT. 10) GO TO 170
      IF (1.0-U .LT. 0.0002) GO TO 190
  170 CONTINUE
  180 XTR=0.5*(XBIG+XLIT)
      U=XTR/SQRT(XTR**2+TKJSQ)
      USQ=U**2
  190 IF (1.0-U .GT. 0.0002) GO TO 220
C-----IF U IS TOO NEAR 1, COMPUTE TDIR AS WAVE ALONG THE TOP OF LAYER JL
      IF (ISW .EQ.1) GO TO 195
      TDC=TID(JL,JL)+DELTA(I)/V(JL)
      GO TO 200
  195 TIX=F(1,JL)*DH1*G(1,JL)+F(2,JL)*DH2*G(2,JL)+TID(JL,JL)
      TDC=TIX+DELTA(I)/V(JL)
  200 IF (JL .EQ. NL) GO TO 210
      IF (TDC .GE. TMIN) GO TO 80
  210 T(I)=TDC
      DTDD=1.0/V(JL)
      DTDH=0.0
      ANIN(I)=0.9999999
      GO TO 260
C-----TRAVEL TIME & DERIVATIVES FOR DIRECT WAVE BELOW FIRST LAYER
  220 TDIR=TKJ/(V(JL)*SQRT(1.0-USQ))
      DO 240 L=1,J1
  240 TDIR=TDIR+(THK(L)*V(JL))/(VSQ(L)*SQRT(VSQ(JL)/VSQ(L)-USQ))
      IF (JL .EQ. NL) GO TO 245
      IF (TDIR .GE. TMIN) GO TO 80
  245 T(I)=TDIR
      SRR=SQRT(1.-USQ)
      SRT=SRR**3
      ALFA=TKJ/SRT
      BETA=TKJ*U/(V(JL)*SRT)
      DO 250 L=1,J1
      STK=(SQRT(VSQ(JL)/VSQ(L)-USQ))**3
      VTK=THK(L)/(VSQ(L)*STK)
      ALFA=ALFA+VTK*VSQ(JL)
  250 BETA=BETA+VTK*V(JL)*U
      DTDD=BETA/ALFA
      DTDH=(1.0-V(JL)*U*DTDD)/(V(JL)*SRR)
      ANIN(I)=U
C-----SET UP PARTIAL DERIVATIVES FOR REGRESSION ANALYSIS ---------------
c  260 X(1,I)=-DTDD*DX(I)/DELTA(I)
c      X(2,I)=-DTDD*DY(I)/DELTA(I)
c      X(3,I)=DTDH
  260 CONTINUE
  300 CONTINUE
      RETURN
      END
