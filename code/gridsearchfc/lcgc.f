      SUBROUTINE LCGC(PHIO,LAMO,PHI,LAM,X,Y)  ! *REV. 19.04.1984 **
C --- LCGC CONVERTS FROM LOCAL COORDINATES X,Y (IN KM) TO GEODETIC
C     COORDINATES, PHI AND LAM
C     CORRECTION IS MADE FOR THE CHANGE OF RADIUS WITH ELLIPTICITY
C     AND FOR THE DIFFERENCE BETWEEN GEODETIC AND GEOCENTRIC
C     COORDINATES
C     ACCURACY SHOULD BE TO WITHIN 10 METERS IN 100 KM FOR
C     LATITUDES UP TO ABOUT 70 DEGREES
C        PHIO - LAT. OF COORDINATE ORIGIN
C        LAMO - LONG. OF COORDINATE ORIGIN
C        PHI - LAT. OF POINT TO CONVERT
C        LAM - LONG. OF POINT TO CONVERT
C        X - X COORDINATE (NORTH) OF CONVERTED POINT
C        Y - Y COORDINATE (EAST) OF CONVERTED POINT
C         COMMON VARIABLES XSIGN AND YSIGN CAN BE USED TO
C         GLOBALLY REVERSE THE SIGNS OF THE OUTPUT X AND Y
C         VARIABLES TO SIMULATE THE CORRECT CONVERSION
C         OF SOUTH AND EAST LATITUDES WHERE SIGNS OF
C         INPUT LATS AND LONS ARE NOT SET CORRECTLY.
C         THESE VARIABLES MUST TAKE VALUES +1. OR -1.
      REAL PHIO(3),LAMO(3),PHI(3),LAM(3)
      COMMON /GC/ XSIGN,YSIGN
      DATA R,E,PO180/6378.163,0.0033670033,0.017453293/
      FR(T) = 1.-E*SIN(T)**2
      CONV(X,Y,Z) = (X+Y/60.+Z/3600.)*PO180
      ZLAT = CONV(PHIO(1),PHIO(2),PHIO(3))
      ZLON = CONV(LAMO(1),LAMO(2),LAMO(3))
      CRLAT = ATAN(E*SIN(2.*ZLAT)/FR(ZLAT))
      ZGCL = ZLAT-CRLAT
      A = FR(ZGCL)
      RHO = R*A
      B = (E*SIN(2.*ZGCL)/A)**2+1.
      C = 2.*E*COS(2.*ZGCL)*A+(E*SIN(2.*ZGCL))**2
      CDIST = C/(A**2*B)+1.
      XLAT = (X*XSIGN)*CDIST/RHO+ZLAT
      XL = XLAT/PO180
      T1 = ABS(XL)+.5E-09
      XL = SIGN(T1,XL)
      PHI(1) = AINT(XL)
      RMIN = (XL-PHI(1))*60.
      PHI(2) = AINT(RMIN)
      PHI(3) = (RMIN-PHI(2))*60.
      YLON = ZLON-(Y*YSIGN)/(RHO*COS(XLAT-CRLAT))
      YL = YLON/PO180
      T1 = ABS(YL)+.5E-09
      YL = SIGN(T1,YL)
      LAM(1) = AINT(YL)
      RMIN = (YL-LAM(1))*60.
      LAM(2) = AINT(RMIN)
      LAM(3) = (RMIN-LAM(2))*60.
      RETURN
      END
